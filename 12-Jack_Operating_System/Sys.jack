/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
		do Memory.init();
		do Math.init();
		do Output.init();
		do Screen.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        // Make an infinite loop
        while (true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, calculation;
		let i = 0;
		while (i < duration) {
			let i = i+1;
            
            let calculation = 0;
            while (calculation < 7) {
                let calculation = calculation + 1;
                do Math.divide(32765, 3);
            }
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR<");
        do Output.printInt(errorCode);
        do Output.printString(">");
        do Sys.halt();
        return;
    }
}
