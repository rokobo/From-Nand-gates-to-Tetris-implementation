/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * The chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
        DMux4Way(in=load, sel=address[13..14], a=LoadRAM1, b=LoadRAM2, c=LoadSCR, d=LoadKBD); // There are 2 selection bits for 3 possible outcomes, therefore we repeat RAM
        Or(a=LoadRAM1, b=LoadRAM2, out=LoadRAM);                                              // Unite the LoadRAM bits because of the repetition in the Demultiplexor

        // We pass the load bits to the appropriate memory sections
        RAM16K(in=in, load=LoadRAM, address=address[0..13], out=OutRAM);
        Screen(in=in, load=LoadSCR, address=address[0..12], out=OutSCR);
        Keyboard(out=OutKBD);
        
        Mux4Way16(a=OutRAM, b=OutRAM, c=OutSCR, d=OutKBD, sel=address[13..14], out=out);      // Use the selection bits to get the output from the correct memory section
}